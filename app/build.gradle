plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.didiglobal.booster'
}

android {
    compileSdk 32
    defaultConfig {
        applicationId "com.jeahwan.origin"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0.0"
        resConfigs "zh"
        ndk {
            //根据需要 自行选择添加的对应cpu类型的.so库。
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }

    //禁止生成依赖元数据 不上play用不到
    dependenciesInfo {
        includeInApk = false
    }

    // 优化编译速度 如果有用到kapt添加如下配置
    kapt {
        useBuildCache = true
        javacOptions {
            option("-Xmaxerrs", 500)
        }
    }

    //签名配置
    signingConfigs {
        config {
            storeFile file('../xxx.jks')
            storePassword "xxx"
            keyAlias "xxx"
            keyPassword "xxx"
        }
    }
    //开启dataBinding
    dataBinding {
        enabled = true
    }
    //图片已压缩 指定aapt不做图片压缩 因为可能会反而增加图片大小
    aaptOptions {
        cruncherEnabled = false
    }
    //关闭lint检查
    lintOptions {
        disable "ResourceType"
        abortOnError false
    }
    //jniLibs目录指向libs目录
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    buildTypes {
        debug {
            // 签名
//            signingConfig signingConfigs.config
        }
        alpha {
            //继承debug配置
            initWith(debug)
            // 混淆
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // ZipAlignEnabled优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
        }
        release {
            //继承alpha配置
            initWith(alpha)
            //关闭debug
            debuggable false
        }
    }
    //配置jdk8支持
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    //support包统一最新版本
    implementation([
            'androidx.core:core-ktx:1.7.0',
            'androidx.appcompat:appcompat:1.4.0',
            'androidx.core:core-splashscreen:1.0.0-alpha02',
            'androidx.constraintlayout:constraintlayout:2.1.2'
    ])
    //RxAndroid
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    //Retrofit2
    implementation([
            'com.squareup.retrofit2:adapter-rxjava3:2.9.0',
            'com.squareup.retrofit2:converter-gson:2.9.0'
    ])
    //网络请求日志打印
    implementation('com.github.ihsanbal:LoggingInterceptor:3.1.0') {
        exclude group: 'org.json', module: 'json'
    }
    //底部tabBar
    implementation 'me.majiajie:pager-bottom-tab-strip:2.4.0'
    //沉浸式状态栏
    implementation 'com.readystatesoftware.systembartint:systembartint:1.0.4'
    //lottie
    implementation 'com.airbnb.android:lottie:4.2.2'
    //Glide
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.12.0'
    //Indicator
    implementation 'com.github.hackware1993:MagicIndicator:1.7.0'
    //shape selector 替代方案
    implementation 'com.noober.background:core:1.5.9'
    //自动换行布局
    implementation 'am.widget:wraplayout:1.2.1'
    //material
    implementation 'com.google.android.material:material:1.5.0'
    //崩溃activity
    implementation 'cat.ereza:customactivityoncrash:2.3.0'
    //沉浸式状态栏
    implementation([
            'com.gyf.immersionbar:immersionbar:3.0.0',
            'com.gyf.immersionbar:immersionbar-ktx:3.0.0',
    ])
    //下拉刷新库
    implementation  'com.scwang.smart:refresh-layout-kernel:2.0.3'      //核心必须依赖
    implementation  'com.scwang.smart:refresh-header-material:2.0.3'    //谷歌刷新头
    implementation  'com.scwang.smart:refresh-footer-classics:2.0.3'    //经典加载
    //版本更新 github搜:CheckVersionLib
    implementation 'com.github.AlexLiuSheng:CheckVersionLib:2.4.1_androidx'
}